apply plugin: 'java'
apply plugin: 'application'

version = "0.1"

mainClassName = "com.xoom.oss.fs.Main"

run {
    if (System.getProperty("run.debug") != null) {
        debug true
    }
    maxHeapSize "1g"
    systemProperties = System.getProperties()
}

ext.versions = [
        feathercon: "1.4.2",
        hamcrest: "1.3",
        jersey: "1.17",
        junit: "4.11",
        logback: "1.0.13",
        slf4j: "1.7.2"
]

ext.libraries = [
        jersey_server: [
                "com.sun.jersey:jersey-core:${versions.jersey}",
                "com.sun.jersey:jersey-json:${versions.jersey}",
                "com.sun.jersey:jersey-server:${versions.jersey}",
                "com.sun.jersey:jersey-servlet:${versions.jersey}",
        ],
        jersey_client: [
                "com.sun.jersey:jersey-core:${versions.jersey}",
                "com.sun.jersey:jersey-json:${versions.jersey}",
                "com.sun.jersey:jersey-client:${versions.jersey}",
        ],
        test: [
                "junit:junit:${versions.junit}",
                "org.hamcrest:hamcrest-all:${versions.hamcrest}"
        ],
        logging: [
                "ch.qos.logback:logback-classic:${versions.logback}",
                "org.slf4j:log4j-over-slf4j:${versions.slf4j}",
                "org.slf4j:jul-to-slf4j:${versions.slf4j}"
        ]
]

dependencies {
    compile libraries.logging
    compile libraries.jersey_server
    compile "com.xoom.oss:feathercon:${versions.feathercon}"
    compile 'org.imgscalr:imgscalr-lib:4.2'
    compile 'javax.servlet:javax.servlet-api:3.0.1'
    compile 'com.drewnoakes:metadata-extractor:2.6.2'
    compile 'com.google.code.gson:gson:2.2.4'

    testCompile libraries.jersey_client
    testCompile libraries.test
}

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/xoom/xoomoss"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}

tasks.withType(Compile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties = System.getProperties()
    testLogging.showStandardStreams = true
}
